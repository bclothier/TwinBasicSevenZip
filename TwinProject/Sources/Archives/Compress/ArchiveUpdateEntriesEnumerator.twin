[ COMCreatable (False) ]
Private Class ArchiveUpdateEntriesEnumerator
    Implements IEnumVARIANT

    Private Type TData
        CurrentIndex As Long
        Entries As ArchiveUpdateEntries
        Keys() As Variant
    End Type
    Private This As TData

    Friend Sub New(Entries As ArchiveUpdateEntries, Keys As Variant, Optional CurrentIndex As Long = 0)
    	This.CurrentIndex = CurrentIndex
        This.Keys = Keys
        Set This.Entries = Entries
    End Sub

    Private Sub Next(ByVal celt As Long, ByRef rgvar As Variant, ByRef pceltFetched As Long) Implements IEnumVARIANT.Next
        If VarPtr(rgvar) = 0 Then
            Err.ReturnHResult = E_INVALIDARG
            Exit Sub
        End If

        Dim Index As Long
        If celt = 1 Then
            If This.CurrentIndex < This.Entries.Count Then
                Index = 1
                Dim Entry As ArchiveUpdateEntry
                If This.Entries.TryGet(CStr(This.Keys(This.CurrentIndex)), Entry) Then
                    Set rgvar = Entry
                    This.CurrentIndex += 1
                End If
            End If
        Else
            Err.ReturnHResult = E_INVALIDARG
            Exit Sub
        End If
        
        If VarPtr(pceltFetched) <> 0 Then
            pceltFetched = Index
        End If

        Err.ReturnHResult = If(celt > Index, S_FALSE, S_OK)
    End Sub

    Private Sub Skip(ByVal celt As Long) Implements IEnumVARIANT.Skip
        This.CurrentIndex += celt
        If This.CurrentIndex > UBound(This.Keys) Then
            This.CurrentIndex = UBound(This.Keys)
        End If
    End Sub

    Private Sub Reset() Implements IEnumVARIANT.Reset
    	This.CurrentIndex = 0
    End Sub

    Private Sub Clone(ByRef ppenum As IEnumVARIANT) Implements IEnumVARIANT.Clone
        Set ppenum = New ArchiveUpdateEntriesEnumerator(This.Entries, This.Keys, This.CurrentIndex)
    End Sub
End Class