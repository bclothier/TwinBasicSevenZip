[ COMCreatable (False) ]
[ ClassId ("400B0095-B6D3-45BE-89C6-85EB4234D63F") ]
Private Class HandlerEnumerator
    Implements IEnumVARIANT

    Private Type TData
        CurrentIndex As Long
        List As Scripting.Dictionary
    End Type
    Private This As TData

    Friend Sub New(List As Scripting.Dictionary, Optional CurrentIndex As Long = 0)
    	This.CurrentIndex = CurrentIndex
        Set This.List = List
    End Sub

    Private Sub Next(ByVal celt As Long, ByRef rgvar As Variant, ByRef pceltFetched As Long) Implements IEnumVARIANT.Next
        If VarPtr(rgvar) = 0 Then
            Err.ReturnHResult = E_INVALIDARG
            Exit Sub
        End If

        Dim Index As Long
        If celt = 1 Then
            If This.CurrentIndex < This.List.Count Then
                Set rgvar = This.List(This.CurrentIndex)
                This.CurrentIndex += 1
                Index = 1
            End If
        Else
            Err.ReturnHResult = E_INVALIDARG
            Exit Sub
        End If
        
        If VarPtr(pceltFetched) <> 0 Then
            pceltFetched = Index
        End If

        Err.ReturnHResult = If(celt > Index, S_FALSE, S_OK)
    End Sub

    Private Sub Skip(ByVal celt As Long) Implements IEnumVARIANT.Skip
        This.CurrentIndex += celt
    End Sub

    Private Sub Reset() Implements IEnumVARIANT.Reset
    	This.CurrentIndex = 0
    End Sub

    Private Sub Clone(ByRef ppenum As IEnumVARIANT) Implements IEnumVARIANT.Clone
        Set ppenum = New HandlerEnumerator(This.List, This.CurrentIndex)
    End Sub
End Class